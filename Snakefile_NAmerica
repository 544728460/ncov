include: "Snakefile"

ruleorder: subsample_namerica > subsample 
ruleorder: adjust_metadata_namerica > adjust_metadata

rule subsample_focus:
    message:
        """
        subsample all sequences into a focal set
        """
    input:
        sequences = rules.mask.output.alignment,
        metadata = rules.download.output.metadata
    output:
        sequences = "results/subsample_focus.fasta"
    params:
        group_by = "division year month",
        sequences_per_group = 300,
        exclude_where = "region!='North America'"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude-where {params.exclude_where}\
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --output {output.sequences}
        """

rule make_priorities:
    message:
        """
        determine priority for inclusion in as phylogenetic context by 
        genetic similiarity to sequences in focal set.
        """
    input:
        alignment = rules.mask.output.alignment,
        metadata = rules.download.output.metadata,
        focal_alignment = rules.subsample_focus.output.sequences
    output:
        priorities = "results/subsampling_priorities.tsv"
    shell:
        """
        python3 scripts/priorities.py --alignment {input.alignment} \
                                   --metadata {input.metadata} \
                                   --focal-alignment {input.focal_alignment} \
                                   --output {output.priorities} 
        """

rule subsample_context:
    message:
        """
        Subsample the non-focal sequences to provide phylogenetic context
        """
    input:
        sequences = rules.mask.output.alignment,
        metadata = rules.download.output.metadata,
        priorities = rules.make_priorities.output.priorities
    output:
        sequences = "results/subsample_context.fasta"
    params:
        group_by = "country year month",
        sequences_per_group = 20,
        exclude_where = "region='North America'"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --priority {input.priorities} \
            --exclude-where {params.exclude_where}\
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --output {output.sequences}
        """

rule subsample_namerica:
    input:
        rules.subsample_focus.output.sequences,
        rules.subsample_context.output.sequences
    output:
        "results/subsampled_alignment.fasta"
    shell:
        """
        cat {input} > {output}
        """

        
rule adjust_metadata_namerica:
    input:
        metadata = rules.download.output.metadata
    params:
        region = "'North America'"
    output:
        metadata = "results/metadata_adjusted.tsv"
    shell:
        """
        python3 scripts/adjust_regional_meta.py \
                                   --region {params.region} \
                                   --metadata {input.metadata} \
                                   --output {output.metadata} 
        """